(ns example.eg.x09-form-change
  (:require
    [tilton.mx.api :refer [dp dpx wmx-iso minfo mget mset! mupdate! mav muv
                           fasc fm* cF cF+ cI cFonce ]]
    [tilton.fmx.api :refer [dart-cb] :as fx]
    ["package:flutter/widgets.dart"
     :refer [Text State StatefulWidget StatelessWidget] :as w]
    ["package:flutter/material.dart" :as m
     :refer [MainAxisAlignment Colors Theme Icon Icons ThemeData runApp AppBar State]]
    ["package:flutter/painting.dart"
     :refer [TextStyle]]))

(defn make-app []
  (let [title "Text field change detection"]
    (fx/material-app {:title title}
      (fx/scaffold
        {:appBar (fx/app-bar {:title (m/Text title)})}
        (fx/padding
          {:padding (m/EdgeInsets.all 16.0)}
          (fx/column
            {:mainAxisAlignment MainAxisAlignment/center}
            (fx/text-field
              {:onChanged (dart-cb [text]
                            (mset! me :value text))}
              {:name  :input-a
               :value (cI nil :watch (fn [prop me newv priorv c]
                                       (dp (str "First text field: " newv))))})
            (fx/text-field
              {:onChanged (dart-cb [text]
                            (mset! me :value text))}
              {:name       :input-b
               :value      (cI "And then...?")
               :controller (cFonce (let [c (m/TextEditingController .text (mget me :value))]
                                     (.addListener c
                                       (fn [] (dp (str "Second text field: " (.-text c)))))
                                     c))})
            (fx/text (str "(a) " (muv :input-a) " and (b) " (muv :input-b)))))))))



