(ns example.eg.x07-animated-container
  (:require
    ["package:flutter/material.dart" :as m]
    [tilton.mx.api :refer [dp dpx wmx-iso minfo mget mset! mupdate! mav muv fm* cF cF+ cI]]
    [tilton.fmx.api :as fx]))

(defn random-to-double [max]
  (-> max rand-int .toDouble))

(defn random-color []
  (m/Color.fromRGBO (rand-int 256)
    (rand-int 256)
    (rand-int 256)
    1))

(defn make-app []
  (fx/material-app
    (fx/scaffold
      {:appBar (fx/app-bar {:title (fx/text "Animated Container Demo")})
       :floatingActionButton
       (cF (fx/floating-action-button
             {:onPressed (fx/dart-cb []
                           (let [anicon (fm* :anicon)]
                              (mupdate! anicon :config assoc
                                :width (random-to-double 300)
                                :height (random-to-double 300)
                                :color (random-color)
                                :borderRadius (m/BorderRadius.circular (random-to-double 100)))))}
             (fx/icon m/Icons.play_arrow)))}

      (fx/center
        (fx/animated-container
          {:width      (cF (:width (mav :config)))
           :height     (cF (:height (mav :config)))
           :decoration (cF (let [config (mav :config)]
                             (m/BoxDecoration
                               .color (:color config)
                               .borderRadius (:borderRadius config))))
           :duration   (Duration .seconds 1)
           :curve      m/Curves.fastOutSlowIn}
          {:name   :anicon
           :config (cI {:width        50.0
                        :height       50.0
                        :color        m/Colors.green
                        :borderRadius (m/BorderRadius.circular 8.0)})})))))