(ns example.eg.flutter-layout-tutorial
  "Port of Dave Liepmann's ClojureDart port https://github.com/daveliepmann/ClojureDart/tree/main/samples/flutter_tutorial_building_layouts
  of Flutter 'Building layouts' tutorial https://docs.flutter.dev/ui/layout/tutorial"
  (:require
    ["package:flutter/material.dart" :as m]
    ["package:flutter/painting.dart" :as p]
    [clojure.string :as str]
    [tilton.mx.api :refer [ dp dpx cF cF+ cI mget mswap! mset! mav fasc fm* muv fmu minfo] :as mx]
    [tilton.fmx.api :as fx
     :refer [scaffold app-bar text center column hero material-app
             container row expanded icon padding within-ctx]]))

(defn make-app []
  (let [title "Flutter layout demo (cljd-ified!)"]
    (material-app
      {:title title}
      (scaffold
        {:appBar (app-bar {:title (text title)})}
        (column
          (m/Image.asset "image/lake-oeschinen.jpeg"
            .width 600 .height 240
            .fit m/BoxFit.cover)

          (title-section)

          (fx/builder                                       ;; supplies extra context to support Theme.of
            (row {:mainAxisAlignment m/MainAxisAlignment.spaceEvenly}
              (into [] (for [[ico label] [[m/Icons.call "CALL"]
                                          [m/Icons.near_me "ROUTE"]
                                          [m/Icons.share "SHARE"]]]
                         (button-column (within-ctx [me ctx]
                                          (.-primaryColor (m/Theme.of ctx)))
                           ico label)))))

          (padding {:padding (m/EdgeInsets.all 32)}
            (text {:softWrap true}
              (campground-desc))))))))

;;; --- sections ------------------------------------------------------

(defn title-section []
  (container
    {:padding (m/EdgeInsets.all 32)}
    (row
      (expanded
        (column {:crossAxisAlignment m/CrossAxisAlignment.start}
          (container {:padding (m/EdgeInsets.only .bottom 8)}
            (text {:style (m/TextStyle .fontWeight m/FontWeight.bold)}
              "Oeschinen Lake Campground"))
          (text {:style (m/TextStyle .color (m/Colors.grey 500))}
            "Kandersteg, Switzerland")))
      (fx/icon {:color (m/Colors.red 500)}
        m/Icons.star)
      (text "41"))))

(defn button-column [color icon label]
  (column {:mainAxisSize      m/MainAxisSize.min
           :mainAxisAlignment m/MainAxisAlignment.center}
    {:name :button-col}
    (fx/icon {:color color} icon)
    (container {:margin (m/EdgeInsets.only .top 8)}
      (text {:style (fx/text-style {:fontSize   12
                                    :fontWeight m/FontWeight.w400
                                    :color      color})}
        label))))

(defn campground-desc []
  (str/replace
    "Lake Oeschinen lies at the foot of the Bl√ºemlisalp in the Bernese Alps.
  Situated 1,578 meters above sea level, it is one of the larger Alpine Lakes.
  A gondola ride from Kandersteg, followed by a half-hour walk through pastures
  and pine forest, leads you to the lake, which warms to 20 degrees Celsius in the summer.
  Activities enjoyed here include rowing, and riding the summer toboggan run."
    #"\n" ""))

