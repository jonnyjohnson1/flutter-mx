(ns example.demo.isolate.x00-compute
  (:require
    ["package:flutter/gestures.dart" :as g]
    ["package:flutter/material.dart" :as m
     :refer [Canvas Size]]
    ["package:flutter/painting.dart" :as p]
    ["package:vector_math/vector_math_64.dart" :as vm]
    ["dart:math" :as math
     :refer [pi cos sin]]
    ["dart:async" :as async
     :refer [Completer StreamSubscription]]
    ["dart:isolate" :as di
     :refer [Isolate SendPort ReceivePort]]
    ["package:flutter/foundation.dart" :as fnd]

    [tilton.mx.cell.base :as cb]
    [tilton.mx.api :as mx
     :refer [dp dpx cI cF cF+ cF+n cFn cFonce mget mget? mset! mswap! minfo fasc fasc-inc
             navig fmu muv fm* mav mx-bound? wmx-iso with-cc]]
    [tilton.fmx.api :as fx]
    [example.demo.isolate.common :as cmn
     :refer [clock-face slow-is-prime? dbgiso]]))

(defn use-compute? [starting]
  (fx/sized-box {:width 300}
    (fx/checkbox-list-tile
      {:controlAffinity m/ListTileControlAffinity.leading
       :title           (m/Text "Use Foundation/compute?"
                          .style (p/TextStyle
                                   .fontSize 24))
       :value           (cI starting)
       :onChanged       (cF #(wmx-iso
                               (mset! me :value %)
                               (mset! (fmu :primer) :of 9000039)))}
      {:name :use-compute?})))

(declare prime-decision)

(defn make-app []
  (let [title "Smooth Clock"]
    (fx/material-app
      {:title title}
      (fx/scaffold
        {:appBar (fx/app-bar {:title (fx/text title)})
         :floatingActionButton
         (fx/floating-action-button
           {:onPressed (fx/dart-cb []
                         (wmx-iso
                           (mswap! (fmu :primer) :of + 2)))}
           (m/Icon m/Icons.play_arrow
             .color m/Colors.black))}
        (fx/column
          {:mainAxisAlignment m/MainAxisAlignment.spaceEvenly}
          (clock-face)
          (use-compute? false)
          (fx/row {:mainAxisAlignment m/MainAxisAlignment.spaceEvenly}
            (fx/text {:style (p/TextStyle .fontSize 24)}
              {:name   :primer
               :of     (cI 9000039)
               :prime? (cF+ [:async? true]
                         (let [n (mget me :of)
                               use? (mx/muv :use-compute?)]
                           (if use?
                             (fnd/compute slow-is-prime? n)
                             (future (slow-is-prime? n)))))}
              "" #_(str "Is " (mget me :of) " prime? "))
            ; ... results:
            (prime-decision))
          (fx/text {:style (p/TextStyle .fontSize 16)}
            "Tap for next in 9000041,.49,.59,.67,.."))))))

(defn prime-decision []
  (fx/sized-box {:height 32}
    (fx/text
      {:style (cF (let [{:keys [prime?]} (mget (fmu :primer) :prime?)]
                    (fx/text-style
                      {:fontSize   24
                       :fontWeight m/FontWeight.w400
                       :color      (if prime? m/Colors.green m/Colors.red)})))}
      {:name :prime-dec-text}
      (let [{:keys [of prime?]} (mget (fmu :primer) :prime?)]
        (dpx :decision-sees of :prime? prime?)
        (str "Is " of " prime? "
          (cond
            (nil? prime?) "..."
            (true? prime?) "Yep!!"
            (false? prime?) "Nope"
            :else "#@!%"))))))
